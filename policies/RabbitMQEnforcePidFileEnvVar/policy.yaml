apiVersion: pac.weave.works/v2beta1
kind: Policy
metadata:
  name: weave.policies.rabbitmq-enforce-pid-file-env-var
spec:
  id: weave.policies.rabbitmq-enforce-pid-file-env-var
  name: RabbitMQ Enforce Environment Variable - RABBITMQ_PID_FILE
  enabled: false
  description: |
    This Policy ensures RABBITMQ_PID_FILE environment variable are in place when using the official container images from Docker Hub.
    RABBITMQ_PID_FILE: File in which the process id is placed for use by rabbitmqctl wait.
  how_to_solve: |
    If you encounter a violation, ensure the RABBITMQ_PID_FILE environment variables is set.
    For futher information about the RabbitMQ Docker container, check here: https://hub.docker.com/_/rabbitmq
  category: weave.categories.organizational-standards
  severity: high
  targets: {kinds: [Deployment, Job, ReplicationController, ReplicaSet, DaemonSet, StatefulSet, CronJob]}
  parameters:
    - name: exclude_namespaces
      type: array
      required: false
      value:
    - name: exclude_label_key
      type: string
      required: false
      value:
    - name: exclude_label_value
      type: string
      required: false
      value:
  code: |
    package weave.advisor.rabbitmq.enforce_pid_file_env_var

    import future.keywords.in

    env_name = "RABBITMQ_PID_FILE"
    app_name = "rabbitmq"
    exclude_namespaces := input.parameters.exclude_namespaces
    exclude_label_key := input.parameters.exclude_label_key
    exclude_label_value := input.parameters.exclude_label_value

    violation[result] {
      isExcludedNamespace == false
      not exclude_label_value == controller_input.metadata.labels[exclude_label_key]
      some i
      containers := controller_spec.containers[i]
      contains(containers.image, app_name)
      not containers.env
      result = {
        "issue detected": true,
        "msg": "environment variables needs to be set",
        "violating_key": sprintf("spec.template.spec.containers[%v]", [i]),  }
    }

    violation[result] {
      isExcludedNamespace == false
      not exclude_label_value == controller_input.metadata.labels[exclude_label_key]
      some i
      containers := controller_spec.containers[i]
      contains(containers.image, app_name)
      envs := containers.env
      not array_contains(envs, env_name)
      result = {
        "issue detected": true,
        "msg": sprintf("'%v' is missing'; detected '%v'", [env_name, envs]),
        "violating_key": sprintf("spec.template.spec.containers[%v].env.name", [i])
      }
    }


    array_contains(array, element) {
      array[_].name = element
    }

    # Controller input
    controller_input = input.review.object

    # controller_container acts as an iterator to get containers from the template
    controller_spec = controller_input.spec.template.spec {
      contains_kind(controller_input.kind, {"StatefulSet" , "DaemonSet", "Deployment", "Job"})
    } else = controller_input.spec {
      controller_input.kind == "Pod"
    } else = controller_input.spec.jobTemplate.spec.template.spec {
      controller_input.kind == "CronJob"
    }

    contains_kind(kind, kinds) {
      kinds[_] = kind
    }

    isExcludedNamespace = true {
    	controller_input.metadata.namespace
    	controller_input.metadata.namespace in exclude_namespaces
    } else = false
